########### next target ###############

set(kerfuffle_SRCS
    archiveformat.cpp
    archive_kerfuffle.cpp
    archiveinterface.cpp
    extractionsettingspage.cpp
    previewsettingspage.cpp
    settingspage.cpp
    jobs.cpp
    adddialog.cpp
    compressionoptionswidget.cpp
    createdialog.cpp
    extractiondialog.cpp
    propertiesdialog.cpp
    queries.cpp
    addtoarchive.cpp
    cliinterface.cpp
    mimetypes.cpp
    plugin.cpp
    pluginmanager.cpp
    archiveentry.cpp
    archiveentry.h
)

kconfig_add_kcfg_files(kerfuffle_SRCS settings.kcfgc)

ki18n_wrap_ui(kerfuffle_SRCS
    createdialog.ui
    extractiondialog.ui
    extractionsettings.ui
    previewsettings.ui
    propertiesdialog.ui
    compressionoptionswidget.ui
)

ecm_qt_declare_logging_category(kerfuffle_SRCS
                                HEADER ark_debug.h
                                IDENTIFIER ARK
                                CATEGORY_NAME ark.kerfuffle)

add_library(kerfuffle SHARED ${kerfuffle_SRCS})
generate_export_header(kerfuffle BASE_NAME kerfuffle)

target_link_libraries(kerfuffle
PUBLIC
    KF5::IconThemes
    KF5::Pty
    KF5::Service
    KF5::I18n
    KF5::WidgetsAddons
PRIVATE
    Qt5::Concurrent
    KF5::KIOCore
    KF5::KIOWidgets
    KF5::KIOFileWidgets
)

set_target_properties(kerfuffle PROPERTIES VERSION ${KERFUFFLE_VERSION_STRING} SOVERSION ${KERFUFFLE_SOVERSION})

install(TARGETS kerfuffle ${KDE_INSTALL_TARGETS_DEFAULT_ARGS} LIBRARY NAMELINK_SKIP)

install(FILES kerfufflePlugin.desktop DESTINATION ${KDE_INSTALL_KSERVICETYPES5DIR})
install(FILES ark.kcfg                DESTINATION ${KDE_INSTALL_KCFGDIR})

install(FILES mime/kerfuffle.xml DESTINATION ${KDE_INSTALL_MIMEDIR})
if(SharedMimeInfo_FOUND)
    update_xdg_mimetypes(${KDE_INSTALL_MIMEDIR})
endif()
